Package sc;


Helpers

lettre = [['a' .. 'z'] + ['A' .. 'Z']];



Tokens

espaces = (' ' | 13 | 10)+;
commentaire= '#' [[0 .. 0xffff] - [10 + 13]]* (10 | 13 | 10 13);
plus = '+';
moins = '-';
mult = '*';
div = '/';
parenth_g = '(';
parenth_d = ')';
crochet_g = '[';
crochet_d = ']';
accolad_g = '{';
accolad_d = '}';
ou = '|';
et = '&';
equals = '=';
inf = '<';
neg = '!';
virgule = ',';
pt_virgule = ';';
ent = 'entier';
if = 'si';
then = 'alors';
else = 'sinon';
while = 'tantque';
do = 'faire';
return = 'retour';
read = 'lire';
write = 'ecrire';
nb = ['0' .. '9']+;
id = (lettre | '$' | '_')(lettre | '$' | '_' | ['0' .. '9']+)*;



Ignored Tokens

espaces, commentaire;



Productions

programme =
    {liste_optdeclaravar} liste_optdeclaravar liste_declarafonc |
    {liste_declarafonc} liste_declarafonc;

liste_optdeclaravar = liste_declaravar pt_virgule;

liste_declarafonc =
    {oneormore} declarafonc liste_declarafonc |
    {last} ;

declarafonc =
    {withvardec} id listeparam liste_optdeclaravar instrbloc |
    {novardec} id listeparam instrbloc;

listeparam =
    {withparam} parenth_g liste_declaravar parenth_d |
    {withoutparam} parenth_g parenth_d;

liste_declaravar =
    {morethanone} declaravar liste_declaravar2 |
    {one} declaravar;

liste_declaravar2 =
    {more} virgule declaravar liste_declaravar2 |
    {last} virgule declaravar;

declaravar =
    {ent} ent id |
    {inttable} ent id crochet_g nb crochet_d;

var =
    {var} id |
    {table} id crochet_g exp crochet_d;

exp =
    {or} exp ou exp1 |
    {exp1} exp1;

exp1 =
    {et} exp1 et exp2 |
    {exp2} exp2;

exp2 =
    {equals} exp2 equals exp3 |
    {inf} exp2 inf exp3 |
    {exp3} exp3;

exp3 =
    {plus} exp3 plus exp4 |
    {moins} exp3 moins exp4 |
    {exp4} exp4;

exp4 =
    {mult} exp4 mult exp5 |
    {div} exp4 div exp5 |
    {exp5} exp5;

exp5 =
    {neg} neg exp5 |
    {parenthesis} parenth_g exp parenth_d |
    {nb} nb |
    {call} call |
    {var} var |
    {read} read parenth_g parenth_d;

liste_exp =
    {morethanone} exp liste_exp2 |
    {none} ;

liste_exp2 =
    {more} virgule exp liste_exp2 |
    {last} ;

instr =
    {affinstr} affinstr |
    {ifinstr} ifinstr |
    {whileinstr} whileinstr |
    {callinstr} callinstr |
    {retinstr} retinstr |
    {writeinstr} writeinstr |
    {emptyinstr} emptyinstr |
    {instrbloc} instrbloc ;

affinstr = var equals exp pt_virgule;

ifinstr =
    {ifthen} if exp then instrbloc | {ifthenelse} if exp then instrbloc elseinstr;

elseinstr = else instrbloc;

whileinstr = while exp do instrbloc;

callinstr = call pt_virgule;

call = id parenth_g liste_exp parenth_d;

retinstr = return exp pt_virgule;

writeinstr = write parenth_g exp parenth_d pt_virgule;

emptyinstr = pt_virgule;

instrbloc = accolad_g instrbloc2 accolad_d;

instrbloc2 =
    {instr} instr instrbloc2 | {noinstr} ;
